# XPP code to recreate bifurcation diagrams
# Copyright: Marsa Taheri and Gregory Handy, 2016

# ODEs
c' = (j_ip3r(c)-j_serca(c)+j_leak(c)+(j_in-j_out-j_pmca+j_soc(c,c_t))*delta)
c_t' = ((j_in-j_out-j_pmca+j_soc(c,c_t))*delta)
h'=((h_inf(c)-h)/tau_h(c))
ip'=r_rise*(heav(t-s1)-heav(t-s1-d_rise))-r_decay*ip+v_vip*(1-v^rr/(k_vip^rr+v^rr))
v'=-(j_cav(c,v)+j_ionic(v))/1.7


aux j_cav=j_cav(c,v)
aux j_ionic=j_ionic(v)
aux ca_er=(c_t-c)*gamma
aux j_serca=j_serca(c)
aux j_leak=j_leak(c)
aux j_ip3r=j_ip3r(c)
aux j_soc=j_soc(c,c_t)
aux j_pmca1=j_pmca
aux j_in1=j_in
aux j_out1=j_out

# Terms on ER
m_inf = ip/(ip+d_1)
n_inf(c) = c/(c+d_5)
h_inf(c) = q_2/(q_2+c)

q_2 = d_2 *(ip+d_1)/(ip+d_3)
tau_h(c) = 1/(a_2*(q_2+c))

j_ip3r(c) = v_ip3r*m_inf^3*n_inf(c)^3*h^3*((c_t-c)*gamma-c)
j_leak(c) = v_leak*((c_t-c)*gamma-c)
j_serca(c) = v_serca*c^1.75/(c^1.75+k_serca^1.75)

# Terms on plasma membrane
j_in = v_in
j_out = k_out*c
j_pmca=v_pmca*c^2/(k_pmca^2 + c^2)
j_soc(c,c_t) = v_soc*k_soc^4/(k_soc^4+((c_t-c)*gamma)^4)

# Terms for voltage
j_cav(c,v) = v_cav*(c/(c+k_cav))*(v-e_cav)
j_ionic(v) = v_ion*(v-e_ion)

# Initial Conditions
init c=0.0865415,h=0.6255124
init c_t=36.49084
init ip=0,v=-60

# IP3 Dynamics
# param ip=0

# param amp=0.2, d_rise=10, r_rise=0.2, d_decay=97
param d_rise=10, r_rise=0.025, r_decay=0.025
param s1=20

param gamma=5.4054

# Leak for ER
param v_leak=0.002

# Leak across plasma membrane
param v_in=0.05, k_out=1.2

# IP3R Parameters
param v_ip3r=0.1
param d_1=.13,d_2=1.049,d_3=.9434,d_5=.08234
param a_2=0.04

# PMCA Terms
param v_pmca=10,k_pmca=2.5

# SOC Terms
param v_soc=1.57,k_soc=90

# SERCA Terms
param v_serca=0.9, k_serca=0.1

# Sneyd Parameter
param delta=0.2

# CaV terms
param v_cav=4.0
param k_cav=1.4
param e_cav=-20

# ionic terms
param v_ion=1.12
param e_ion=-67.2

# v-ip3 terms
param v_vip=0.8
param k_vip=-58
param rr=8


@ ylo=0,ds=0.005,dsmin=0.001,dsmax=0.01,nmax=700,npr=700
@ autoymin=0,autoymax=.708,parmax=100,autoxmin=0,autoxmax=.5
@ total=1000,xhi=100,ylo=0,yhi=1.5,nmesh=100

@ bounds=1000

done
